//(c) 2015-2017 XolioWare Interactive
//http://chunkstories.xyz
//http://xol.io

allprojects {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
}

//version = '2017.09.23'
version = 'Alpha1.0rc5_03'
description = 'A voxel game and game engine designed for modders'
group = 'io.xol.chunkstories'

ext {
	csApiBuiltAgainst = '113'
}

task buildAll {
	dependsOn ':client:shadowJar'
	dependsOn ':server:shadowJar'
	dependsOn ':converter:shadowJar'
	
	dependsOn ':core:jar'
	dependsOn ':common:jar'

	dependsOn ':launcher:createExe'
}

task uploadAll {
	dependsOn ':client:uploadShadow'
	dependsOn ':server:uploadShadow'
	//dependsOn ':converter:shadowJar'
	
	dependsOn ':core:uploadArchives'
	dependsOn ':common:uploadArchives'
}

//For all subprojects...
subprojects {
	//Put them all in the io.xol.chunkstories group.
	group = rootProject.group

	plugins.withType(JavaPlugin) {
		//Setups compilation args
		compileJava.options.compilerArgs << '-Xlint:-deprecation'
		compileJava.options.compilerArgs << '-Xlint:none'
		
		//Global dependencies
		dependencies {
			compile group: 'org.fusesource.jansi', name: 'jansi', version: '1.15'
			compile group: 'org.l33tlabs.twl', name: 'pngdecoder', version: '1.0'
			compile group: 'net.jpountz.lz4', name: 'lz4', version: '1.3.0'
			compile group: 'com.googlecode.soundlibs', name: 'jorbis', version: '0.0.17.4'
			compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
			
			compile group: 'io.xol.chunkstories', name: 'api', version: csApiBuiltAgainst
			
			testCompile 'junit:junit:4.12'
		}
	}

	//Makes this available everywhere
	ext {
		csApiBuiltAgainst = rootProject.csApiBuiltAgainst
	}
	
	//Adds this debug task
	task artifacts {
		group = "Help"
			description = "Displays the artifacts associated with each configuration of " + project
			doFirst {
				configurations.findAll().each { config ->
				println "${config}:"
				config.artifacts.getFiles().each { file -> println "" + file}
				println ' '
			}
		}
	}

	configurations {
		deployerJars
	}

	dependencies {
		deployerJars "org.apache.maven.wagon:wagon-ssh:2.12" //<-- fuck this fuck this fuck this
	}
}

